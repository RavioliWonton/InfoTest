import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    id 'com.android.application'
    id 'org.gradle.android.cache-fix'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'project-report'
    id 'dev.zacsweers.moshix' version '0.27.1'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'com.getkeepsafe.dexcount' version '4.0.0'
    id 'androidx.baselineprofile'
}

android {
    compileSdkPreview "VanillaIceCream"
    buildToolsVersion = "35.0.0-rc4"

    defaultConfig {
        applicationId "com.example.infotest"
        minSdk 21
        targetSdkPreview "VanillaIceCream"
        versionCode 7
        versionName "1.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
        resourceConfigurations += ["zh"]
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    compileOptions {
        incremental true
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JvmTarget.JVM_17.target
        languageVersion = KotlinVersion.KOTLIN_2_0.version
        apiVersion = KotlinVersion.KOTLIN_2_0.version
        javaParameters = true
    }

    buildFeatures {
        compose true
    }

    packaging {
        resources.excludes += '/META-INF/{AL2.0,LGPL2.1}'
        resources.excludes += ['DebugProbesKt.bin']
        //resources.excludes += 'META-INF/versions/9/previous-compilation-data.bin'
    }

    namespace 'com.example.infotest'

    testCoverage {
        jacocoVersion = '0.8.11'
    }

    lint {
        abortOnError false
        checkAllWarnings true
        checkDependencies true
        explainIssues true
        showAll true
        checkReleaseBuilds false
    }
}

dependencies {
    baselineProfile projects.baselineprofile
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.0.4'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.13'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.louiscad.splitties:splitties-appctx:3.0.0'
    implementation 'androidx.core:core-ktx:1.15.0-alpha01'
    implementation 'androidx.versionedparcelable:versionedparcelable:1.2.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.3.0-alpha02'
    implementation 'androidx.fragment:fragment-compose:1.8.0'
    implementation 'androidx.collection:collection:1.4.0'
    implementation 'androidx.customview:customview:1.2.0-alpha02'
    implementation 'androidx.profileinstaller:profileinstaller:1.4.0-alpha01'
    implementation 'androidx.viewpager:viewpager:1.1.0-alpha01'
    implementation 'androidx.privacysandbox.ads:ads-adservices:1.1.0-beta07'
    implementation "androidx.concurrent:concurrent-futures-ktx:$androidx_concurrent_future_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material3:material3:$compose_material3_version"
    //implementation "androidx.compose.material3:material3-window-size-class:$compose_material3_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation 'androidx.window:window:1.3.0'
    implementation 'androidx.loader:loader:1.1.0'
    implementation "androidx.annotation:annotation:$androidx_annotation_version"
    implementation "androidx.annotation:annotation-experimental:$androidx_annotation_experimental_version"
    implementation "com.squareup.okio:okio:$okio_version"
    implementation 'com.squareup.moshi:moshi:1.15.1'
    //noinspection GradleDependency
    implementation 'com.tencent:mmkv:1.3.4' // 1.3.5 drop minSdk < 23 and armv7 support
    implementation 'com.getkeepsafe.relinker:relinker:1.4.5'
    //implementation 'com.guardsquare:proguard-retrace:7.4.1'
    implementation 'com.huawei.hms:ads-identifier:3.4.71.301'
    implementation 'com.google.android.gms:play-services-cronet:18.1.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.gms:play-services-appset:16.1.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutine_version"
    implementation 'com.github.instacart:truetime-android:4.0.0.alpha'
    implementation 'com.jakewharton.byteunits:byteunits:0.9.1'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    testImplementation 'junit:junit:4.13.2'
    implementation "androidx.tracing:tracing-ktx:$tracing_version"
    androidTestImplementation "androidx.test.ext:junit:$test_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}

tasks.named("dependencyUpdates").configure {
    checkConstraints = true
    checkBuildEnvironmentConstraints = true
}