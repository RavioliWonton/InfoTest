import com.android.build.api.dsl.ManagedVirtualDevice
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.test)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.androidx.baselineprofile)
}

android {
    namespace = 'com.example.baselineprofile'
    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = JvmTarget.JVM_21.target
    }

    defaultConfig {
        minSdk = 28
        targetSdk = 36

        testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments["androidx.benchmark.fullTracing.enable"] = "true"
    }

    targetProjectPath = ':app'
    // Enable the benchmark to run separately from the app process
    experimentalProperties["android.experimental.self-instrumenting"] = true

    testOptions.managedDevices.devices {
        pixel9Api36(ManagedVirtualDevice) {
            device = 'Pixel 9'
            apiLevel = 36
            systemImageSource = 'google'
        }
        pixel6Api35(ManagedVirtualDevice) {
            device = 'Pixel 6'
            apiLevel = 35
            systemImageSource = 'google-atd'
        }
        pixel8Api34(ManagedVirtualDevice) {
            device = 'Pixel 8'
            apiLevel = 34
            systemImageSource = 'google-atd'
        }
    }
}

// This is the configuration block for the Baseline Profile plugin.
// You can specify to run the generators on a managed devices or connected devices.
baselineProfile {
    managedDevices += ["pixel6Api35", "pixel8Api34", "pixel9Api36"]
    useConnectedDevices = false
}

dependencies {
    implementation libs.kotlin.stdlib.jdk8
    implementation libs.androidx.tracing.ktx
    implementation libs.androidx.tracing.perfetto
    implementation libs.androidx.tracing.perfetto.binary
    implementation libs.androidx.annotation
    implementation libs.androidx.annotation.experimental
    implementation libs.androidx.concurrent.futures.ktx
    implementation libs.androidx.lifecycle.common.java8
    implementation libs.androidx.test.junit
    implementation libs.androidx.test.espresso.core
    implementation libs.androidx.test.uiautomator
    implementation libs.androidx.test.rules
    implementation libs.androidx.benchmark.macro.junit4
    implementation libs.okio
    implementation libs.kotlinx.coroutines.test
}